set dotenv-load

# Use `just work 01 1` to work on the specific binary for a specific day's problems
work day part:
    cargo watch -w day-{{day}} -x "check -p day-{{day}}" -s "just test day-{{day}} part{{part}}" -s "just lint day-{{day}}"
lint day:
    cargo clippy -p day-{{day}}
test day part:
    cargo nextest run -p day-{{day}} part{{part}}
bench-all:
    cargo bench -q > benchmarks.txt
bench day part:
    cargo bench --bench day-{{day}}-bench {{part}} >> day-{{day}}.bench.txt
# create the directory for a new day's puzzle and fetch the input
create day:
    cargo generate --path ./daily-template --name day-{{day}}
    just get-input day-{{day}}

# You can find SESSION by using Chrome tools:
# 1) Go to https://adventofcode.com/2022/day/1/input
# 2) right-click -> inspect -> click the "Application" tab.
# 3) Refresh
# 5) Click https://adventofcode.com under "Cookies"
# 6) Grab the value for session. Fill it into your .env file
# 
# example .env:
#
# ```
# SESSION=PASTE_COOKIE_VALUE_HERE
# ```
#
# get the input for a day's puzzle
get-input day:
    ./scripts/get-aoc-input.rs --day day-{{day}} --current-working-directory {{justfile_directory()}}